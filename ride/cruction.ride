{-# STDLIB_VERSION 3 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}
let auctionDuration = 1440 * 30 
# Maximum auction duration set for 30 days
@Callable(i)
func registration(userName: String) = {
    WriteSet([
        DataEntry( i.caller.bytes.toBase58String(),userName)
    ])
} 
@Callable(i)
func beginAuction(minPrice: Int, duration: Int) = {
    if (duration > auctionDuration) then 
        throw("Specified duration(" + toString(duration) +") exceeds the maximum duration(" + toString(auctionDuration) +")") 
    else {  
           let auctionId = toBase58String(i.transactionId)
           let endTime = lastBlock.height + duration
           let pmt = extract(i.payment)
           WriteSet( 
            [   
                DataEntry(auctionId, auctionId),
                DataEntry(auctionId + "_endTime", endTime), 
                DataEntry(auctionId + "_owner", i.caller.bytes.toBase58String()), 
                DataEntry(auctionId + "_itemId", if (isDefined(pmt.assetId)) then toBase58String(value(pmt.assetId)) else throw("Item not atached")),
                DataEntry(auctionId + "_minPrice", minPrice)
            ])
    }
} 
@Callable(i)
func bid(auctionId: String) = {
    let pmt = extract(i.payment)
    let callerAddress = i.caller.bytes.toBase58String()
    let endTime = getIntegerValue(this, auctionId + "_endTime")
    let minPrice = getIntegerValue(this, auctionId + "_minPrice")
    let highestBidAmt = getInteger(this, auctionId + "_highestBidAmt")
    let highestBidder = getString(this, auctionId + "_highestBidder")
    let thisAuctionFrozenBalance = getInteger(this, auctionId + callerAddress + "_frozenBalance")
    let totalBidAmount = pmt.amount + if(isDefined(thisAuctionFrozenBalance)) then value(thisAuctionFrozenBalance) else 0
    if (!isDefined(getString(this, auctionId))) then
        throw("Invalid auction Id") 
    else{ 
        if (lastBlock.height >= endTime) then 
            throw("Auction Completed") 
        else{
            if ((!isDefined(highestBidAmt) && totalBidAmount <= minPrice) || (isDefined(highestBidAmt) && totalBidAmount <= value(highestBidAmt))) then 
                throw("Bid must be more then " + if (!isDefined(highestBidAmt)) then toString(minPrice) else toString(value(highestBidAmt) ) )
            else{
                 WriteSet([
                    DataEntry(auctionId + "_highestBidder", callerAddress),
                    DataEntry(auctionId + "_highestBidAmt", totalBidAmount),
                    DataEntry(auctionId + callerAddress + "_frozenBalance",totalBidAmount)
                ])
            }
        }
    }
}
@Callable(i)
func ownerWithdraw(auctionId: String) = {
    let callerAddress = i.caller.bytes.toBase58String()
    let endTime = getIntegerValue(this, auctionId + "_endTime")
    let owner = getString(this, auctionId + "_owner")
    let highestBidder = getString(this, auctionId + "_highestBidder")
    let itemId = getStringValue(this, auctionId + "_itemId") 
    let highestBidAmt = getInteger(this, auctionId + "_highestBidAmt")
    let ownerAddr = addressFromStringValue(value(owner))
    let itemAddr = fromBase58String(itemId)
    if (callerAddress != owner || !isDefined(getString(this,auctionId)) ) then 
        throw(if (!isDefined(getString(this,auctionId))) then "Invalid auction Id" else "Access Denied")
    else{
        if (lastBlock.height < endTime) then 
            throw("Auction is still running") 
        else{
            if (!isDefined(highestBidder)) then
                if(isDefined(getBoolean(this,auctionId + "_ownerWithdrawStatus"))) then
                    throw("Item already withdrawn")
                else{
                    ScriptResult(
                        WriteSet([DataEntry(auctionId + "_ownerWithdrawStatus", true)]),
                        TransferSet([ScriptTransfer(ownerAddr, 1, itemAddr)])
                    )
                }
            else {
                if (isDefined(getBoolean(this, auctionId + "_ownerWithdrawStatus"))) then
                    throw("The bid amount is already transfered")
                else{
                    ScriptResult(
                        WriteSet([DataEntry(auctionId + "_ownerWithdrawStatus", true)]),
                        TransferSet([
                            ScriptTransfer(ownerAddr, (value(highestBidAmt) / 100) * 95, unit)
                        ])
                    )
                }
            }
        }
    }
}

@Callable(i)
func winnerWithdraw(auctionId: String) = {
    let callerAddress = i.caller.bytes.toBase58String()
    let endTime = getIntegerValue(this, auctionId + "_endTime")
    let highestBidder = getString(this, auctionId + "_highestBidder")
    let itemId = getStringValue(this, auctionId + "_itemId") 
    let highestBidAmt = getInteger(this, auctionId + "_highestBidAmt")
    let highestBidderAddr = addressFromStringValue(value(highestBidder))
    let itemAddr = fromBase58String(itemId)
    if (!isDefined(getString(this,auctionId))) 
        then throw("Invalid auction Id") 
    else{ 
        if ((callerAddress != if (isDefined(highestBidder)) then value(highestBidder) else throw("Nobody won this item"))  ) 
            then throw("Access Denied")
        else{
            if (lastBlock.height < endTime) then 
                throw("Auction is still running") 
            else{
                if (isDefined(getBoolean(this, auctionId + "_winnerWithdrawStatus"))) then
                throw("The item already transfered")
                else{
                    ScriptResult(
                        WriteSet([DataEntry(auctionId + "_winnerWithdrawStatus", true)]),
                        TransferSet([
                            ScriptTransfer(highestBidderAddr, 1, itemAddr)
                        ])
                    )
                }
            }
        }
    }    
}

@Callable(i)
func participantWithdraw(auctionId: String) = {
    let callerAddress = i.caller.bytes.toBase58String()
    let endTime = getIntegerValue(this, auctionId + "_endTime")
    let highestBidder = getString(this, auctionId + "_highestBidder")
    let thisAuctionFrozenBalance = getInteger(this, auctionId + callerAddress + "_frozenBalance")
    if (!isDefined(getString(this,auctionId))) 
        then throw("Invalid auction Id") 
    else{ 
        if (lastBlock.height < endTime) then 
                throw("Auction is still running")
        else{
           if (callerAddress == if (isDefined(highestBidder)) then value(highestBidder) else 0 ) then 
                throw("Winner can't withdraw the frozen balance")
            else{
                if(!isDefined(thisAuctionFrozenBalance) || (thisAuctionFrozenBalance == 0)) then 
                    throw("No Balance")
                else{
                    ScriptResult(
                        WriteSet(
                            [DataEntry(auctionId + callerAddress + "_frozenBalance",0)]),
                        TransferSet([
                                ScriptTransfer( addressFromStringValue(callerAddress),value(thisAuctionFrozenBalance), unit)
                            ])
                        )
                }
            }
        }
    }    
}